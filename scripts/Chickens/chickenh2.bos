#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  body, head, tail, lthigh, lknee, lshin, lfoot, rthigh,
       rknee, rshin, rfoot, lforearm, lupperarm, lblade, rforearm, rupperarm,
       rblade, aimy1;

static-var   Static_Var_2, Static_Var_3;

// Signal definitions
#define SIG_AIM				2


static-var maxSpeed, animFramesPerKeyframe, bMoving, animSpeed;

// For N:\animations\Chickens\chicken_walk_remaster_v2.blend Created by https://github.com/Beherith/Skeletor_S3O V((0, 3, 5))
#define MOVESCALE 100 //Higher values are bigger, 100 is default
#define MOVESPEED 6
#define animAmplitude 66
#define LUHAND lupperarm  //define these as the left and right head thingies
#define RUHAND rupperarm  
#define LLHAND lupperarm  //define these as the left and right head thingies
#define RLHAND rupperarm  
#define SIG_WALK 64 //these defines are needed before includes!
#include "chicken_walk_2legged_v2.h"


#define IDLEMOVESCALE 100
#define IDLEAMPLITUDE 100
#define IDLESPEED 15
#include "chicken_idle_2legged_v2.h"


#define DEATHSCALE 100 //Higher values are bigger, 100 is default
#define DEATHAMPLIDUTE 100 //Higher values are bigger, 100 is default
#define DEATHSPEED 10
#include "chicken_death_2legged_v2.h"
//use call-script DeathAnim(); from Killed()



#define BASEPIECE body //needed for HitByWeapon
#define HITSPEED <25.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 10
#define MAXTILT 100
#include "chicken_hitbyweapon.h"


// REMEMBER TO animspeed = 6 in Create() !!
UnitSpeed(){
	maxSpeed = get MAX_SPEED; // this returns cob units per frame i think
	animFramesPerKeyframe = MOVESPEED; //we need to calc the frames per keyframe value, from the known animtime
	maxSpeed = maxSpeed + (maxSpeed /(2*animFramesPerKeyframe)); // add fudge
	while(TRUE){
		animSpeed = (get CURRENT_SPEED);
		if (animSpeed<1) animSpeed=1;
		animSpeed = (maxSpeed * MOVESPEED) / animSpeed; 
		//get PRINT(maxSpeed, animFramesPerKeyframe, animSpeed); //how to print debug info from bos
		if (animSpeed<3) animSpeed=3;
		if (animspeed> 2* MOVESPEED) animSpeed = 2 * MOVESPEED;
		sleep 197;
	}
}
StartMoving(){
	signal SIG_WALK;
	bMoving=TRUE;
	start-script Walk();
}
#include "chicken_death_2legged_ptaq.h"

static-var isDying;
#include "chicken_killed_2legged.h"

StopMoving(){
	signal SIG_WALK;
	bMoving=FALSE;
	if (!isDying){
		call-script StopWalking();
		start-script Idle();
	}
}

Create()
{
	isDying = FALSE;
	emit-sfx 1024 + 2 from body;
	animSpeed = MOVESPEED;
	start-script UnitSpeed();
	return (0);
}

SweetSpot(piecenum)
{
	piecenum = body;
	return (0);
}

AimFromPrimary(piecenum)
{
	piecenum = head;
	return (0);
}

QueryPrimary(piecenum)
{
	piecenum = head;
	return (0);
}

RestoreAfterDelay()
{
	sleep 1000;
	
	turn aimy1 to y-axis <0> speed <180>;
	turn aimy1 to x-axis <0> speed <180>;
	return (0);
}

AimPrimary(heading, pitch)
{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	if (heading > <60> ) heading = <60>;
	if (heading < <-60> ) heading = <-60>;
	if (pitch > <60> ) pitch = <60>;
	if (pitch < <-60> ) pitch = <-60>;
	turn aimy1 to y-axis heading speed <250.000000>;
	turn aimy1 to x-axis <0.000000> - pitch speed <200.000000>;
	wait-for-turn aimy1 around y-axis;
	
	start-script RestoreAfterDelay();
	return (1);
}

Shot1()
{
	turn lforearm to y-axis <-140.000000> speed <600.000000>;
	turn rforearm to y-axis <140.000000> speed <600.000000>;
	turn lblade to y-axis <140.000000> speed <600.000000>;
	turn rblade to y-axis <-140.000000> speed <600.000000>;
	wait-for-turn rblade around y-axis;
	turn lforearm to y-axis <0.000000> speed <120.000000>;
	turn rforearm to y-axis <0.000000> speed <120.000000>;
	turn lblade to y-axis <0.000000> speed <120.000000>;
	turn rblade to y-axis <0.000000> speed <120.000000>;
	return (0);
}

